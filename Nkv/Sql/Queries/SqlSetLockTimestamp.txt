alter procedure nkv_Set{0}LockTimestamp
(
    @key varchar(128),       
    @version bigint,
    @isLock tinyint
) as
begin
    declare @outputTable table ([RowTimestamp] datetime, [RowVersion] bigint);
    declare @rowTimestamp datetime = null;
	declare @rowVersion bigint;
    declare @rowCount int = 0;
    declare @ackCode varchar(32);

    update [{0}] set 
        [LockTimestamp] =
			case
				when @isLock = 1 then sysutcdatetime()
				else null
			end,
        [Timestamp] = sysutcdatetime(),
		[Version] = [Version] + 1
    output inserted.[Timestamp], inserted.[Version] into @outputTable
    where [Key] = @key and [Version] = @version;

    set @ackCode = 'Success';

    select @rowCount = count(1) from @outputTable;
    select top 1 @rowTimestamp = [RowTimestamp], @rowVersion = [RowVersion] from @outputTable;

    if @rowCount <> 1
    begin
        set @rowTimestamp = null;
        select @rowTimestamp = [Timestamp], @rowVersion = [Version] from [{0}] where [Key] = @key;

        set @ackCode =
        case
            when @rowTimestamp is null then 'KeyNotFound'
            when @version <> @rowVersion then 'VersionMismatch'
            else 'Unknown'
        end;
    end

	select @rowCount [RowCount], @ackCode [AckCode], @rowTimestamp [Timestamp], @rowVersion [Version];
end