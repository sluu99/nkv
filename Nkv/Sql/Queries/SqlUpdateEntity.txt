alter procedure nkv_Update{0}Entity
(
	@key nvarchar(128),
	@value nvarchar(max),
	@timestamp datetime,
	@ignoreLock tinyint = 0
) as
begin
	declare @outputTable table ([RowTimestamp] datetime);
	declare @rowTimestamp datetime = null;
	declare @lockTimestamp datetime = null;
	declare @rowCount int = 0;
	declare @ackCode varchar(32);

	update [{0}] set
		[Value] = @value,
		[Timestamp] = sysutcdatetime()
	output
		inserted.[Timestamp] into @outputTable
	where [Key] = @key and [Timestamp] = @timestamp and (@ignoreLock = 1 or [LockTimestamp] is null);
		
	set @ackCode = 'Success';

	select @rowCount = count(1) from @outputTable;
	select top 1 @rowTimestamp = [RowTimestamp] from @outputTable;

	if @rowCount <> 1
	begin
		set @rowTimestamp = null;
		select @rowTimestamp = [Timestamp], @lockTimestamp = [LockTimestamp] from [{0}] where [Key] = @key;
	
		select @ackCode = 
		case
			when @rowTimestamp is null then 'KeyNotFound'
			when @timestamp <> @rowTimestamp then 'TimestampMismatch'
			when @lockTimestamp is not null then 'EntityLocked'
			else 'Unknown'
		end;
	end

	select @rowCount [RowCount], @rowTimestamp [Timestamp], @ackCode [AckCode];
end